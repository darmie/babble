paquet test1 (
    soit additionneur = (a:int):(b:int):int -> (
        retourne (b:int):int -> (
            retourne a + b
        )
    )

    soit additionne = (a:int, b:int):int -> (
        retourne a + b
    )

    soit factorielle = (n:int):int -> (
        si n < 2 alors (
            retourne 1
        ) sinon (
            retourne n * recurrence(n - 1)
        )
    )

    soit factorielle2 = (n:int):int -> (
        soit resultat = 1
        tant que n > 1 alors (
            resultat = resultat * n ; n = n - 1
        )
        resultat
    )

    soit applique = (a, f=(v) -> (v)) -> ( f(a) )

    def compteur = () -> (
        def c = 0

        () -> (
            c = c + 1
        )
    )

    def compteur2 = () -> (
        retourne objet (
            def v = 0
            def inc = () -> (
                v = v + 1
            )
        )
    )
)

soit fonction_principale = () -> (
    (
        soit qui = "tout le monde !"
        afficherc ("Bonjour ", qui)
    )

    afficherc ("1+2=", test1.additionne(a:1,b:2))

    soit ajoute2 = test1.additionneur(2)
    soit resultat = ajoute2(2)

    retourne "ok:" + resultat + ":" + test1.factorielle(5) + ":" + test1.factorielle2(6)
)

suppose (1<2, "Quelque chose ne va pas")

suppose (test1.applique(1) == 1, "applique ne fonctionne pas")
suppose (test1.applique(1, (v) -> (v+1)) == 2, "applique ne fonctionne pas")

suppose (fonction_principale() == "ok:4:120:720", "Test echou√©")

def c = test1.compteur()
suppose(c() == 1)
suppose(c() == 2)
suppose(c() == 3)

def c2 = test1.compteur2()
suppose(c2.inc() == 1)
suppose(c2.inc() == 2)
suppose(c2.inc() == 3)

def liste = java.util.ArrayList()
suppose(liste.size() == 0)
